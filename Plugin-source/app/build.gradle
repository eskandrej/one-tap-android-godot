plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

if (!file("${project.rootDir.absolutePath}\\app\\libs\\godot-lib.release.aar").exists()) {
    throw new GradleException(
            "Missing Godot AAR library for Android." +
            " Download Library at https://godotengine.org/download" +
            " and rename it to godot-lib.release.aar.\n" +
                    "Place file in ${project.rootDir.absolutePath}\\app\\libs")
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 19
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

task copyLib(type: Copy) {
    from "${buildDir.absolutePath}/outputs/aar/"
    into "${project.rootDir.absolutePath}/../Plugin"
    rename { String fileName -> fileName = "one-tap-android-godot.aar"}
}

task copyPluginToDemo(type: Copy) {
    from "${project.rootDir.absolutePath}/../Plugin"
    into "${project.rootDir.absolutePath}/../Demo/android/plugins"
}


task cleanPluginFolder(type: Delete) {
    delete fileTree("${project.rootDir.absolutePath}/../Plugin").matching {
        exclude 'one-tap-android-godot.gdap'
    }
}

task cleanPluginDemo(type: Delete) {
    delete "${project.rootDir.absolutePath}/../Demo/android/plugins/"
}

afterEvaluate {
    assembleDebug.finalizedBy('copyPluginToDemo')
    assembleRelease.finalizedBy('copyPluginToDemo')
}

clean.dependsOn cleanPluginFolder,cleanPluginDemo
copyPluginToDemo.dependsOn copyLib

dependencies {
    compileOnly fileTree(dir:"libs", include: ["godot-lib.release.aar"])
    implementation 'com.google.android.gms:play-services-auth:20.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}